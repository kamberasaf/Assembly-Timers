#include "bsp.h"
;------------------------------------------------------------------------------
; already in use: R4,R5,R6,R7,R8,R9,R11,R12,R13,R14,R15 ... R10= freq result

           MODULE BSP
           PUBLIC GPIOconfig,Timer_A1_Setup,Stop_Timer_A1,onesecdelay,Stop_Timer_A0
           PUBLIC Timer_A1_Setup_3,state3delay
           EXTERN Delay,delay_ms,delay200usec,LCD_START
          
           RSEG CODE
;-------------------------------------------------------------------------------           
;                      GPIO Configurations
;-------------------------------------------------------------------------------
GPIOconfig
          ;Push Buttons Configutarion
          BIC.B    #0xF7,PBSEL     ; 1110-0111 for I/O, P2.4 and P2.3 are not.
          BIC.B    #0x13,PBDIR
          BIS.B    #0x03,PBEdgeT   ; pull-up - rising edge.
          BIC.B    #0x10,PBEdgeT   ;pull-down P2.4
          BIS.B    #0x13,PBIntEn   
          BIC.B    #0xFF,PBIntFlag
          
          ;LCD Configuration
          BIC.B    #0xFF,LCData
          BIC.B    #0xFF,LCData_SEL     ; LCD Data
          BIS.B    #0xFF,LCData_DIR
          BIC.B    #0xE0,LCDCtrl_SEL   ; LCD Control buttons
          BIS.B    #0xE0,LCDCtrl_DIR
          BIC.B    #0xE0,LCDCtrl        ; clear output
          CALL     #LCD_START

          ;P2.4,P2.3 configuration
          BIS.B   #TB_CLOCK,PBSEL  ;P2.3 SEL=1
          BIS.B   #2,PBSEL         ;P2.2 SEL=1
          BIS.B   #2,PBDIR         ;P2.2 DIR=1
          RET
;-----------------------------------------------------------------------------
; Timer Frequency Setup
;------------------------------------------------------------------------------ 
Timer_A1_Setup    MOV.W   #CM_1+SCS+CAP+CCIE+CCIS_0,Timer_A1_CC2_Ctl  ; Capture on rising edges ;
                  MOV.W   #TACLR,Timer_A1_Ctl                       ; Clear Timer A
                  MOV.W   #TASSEL_2+MC_2+ID_3,Timer_A1_Ctl          ; SMCLK,Continuous Mode, Division=3              
                  MOV.W   #TACLR,Timer_A1_Ctl
                  RET

Timer_A1_Setup_3  MOV.W   #TACLR,Timer_A1_Ctl                       ; Clear Timer A
                  MOV.W   #TASSEL_2+MC_1+ID_3,Timer_A1_Ctl          ; SMCLK,Up Mode, Division=3              
                  MOV.W   #65,Timer_A_CC0_Reg
                  RET
                  

Stop_Timer_A1     BIC.W #TASSEL_2+MC_3,Timer_A1_Ctl ;Stop Timer A1 (Task 2)
                  RET
                                
;-------------------------------------------------------------------------------
;                         Delays - Timer A0
;-------------------------------------------------------------------------------
onesecdelay  
             BIS    #CCIE,Timer_A_CC0_Ctl             ; INTERRUPT ENABLED
             MOV.W  #0xFFFE,Timer_A_CC0_Reg            ; Every time we get to FFFE do a trigger
             BIS.W  #TASSEL_2+MC_3+ID_3,Timer_A_Ctl ; SMCLK,UP DOWN MODE, div=3
             BIS.W  #CPUOFF+GIE,SR             ; SLEEP
             RET   

state3delay 
             BIS    #CCIE,Timer_A_CC0_Ctl             ; INTERRUPT ENABLED
             MOV.W  #0xC000,Timer_A_CC0_Reg            ; Every time we get to C000 do a trigger
                                                       ; 0.75 sec delay
             BIS.W  #TASSEL_2+MC_3+ID_3,Timer_A_Ctl ; SMCLK,UP DOWN MODE, div=3, 2^17 Fclk
             BIS.W  #CPUOFF+GIE,SR             ; SLEEP
             RET
             
Stop_Timer_A0     BIC.W #TASSEL_2+MC_3,Timer_A_Ctl ;Stop Timer (Task 1)
                  BIC   #CCIE,Timer_A_CC0_Ctl
                  RET
                  
                  ENDMOD
                  END